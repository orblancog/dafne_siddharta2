! ~/mad/tools/twiss_select.madx

! set,format="-18s","11.5g";  ! my standard compact format
! set,format="-20s","13.7g";  ! my standard compact format  --  2 digits more useful for exact tune matching
!  set,format="-20s","23.17g"; ! full precision

if( twiss_select_all==1 )
{
  twiss_select_magn:=1;
  twiss_select_aper:=1;
  twiss_select_sol_corr:=1;
  twiss_select_rf  :=1;
  twiss_select_Wfun:=1;
  twiss_select_rmat:=1;
  // twiss_select_coup:=1; // do not select same time as sector map
  select_sect:=1;
}

if( twiss_select_MDISim==1 )
{
  twiss_select_magn:=1;
  twiss_select_aper:=1;
  twiss_select_sol_corr:=1;
  twiss_select_rf  :=1;
}

select, flag = twiss, clear;

select, flag=twiss, column=name,keyword,s,l,SBfield,grad,
grads,grado,gradd,grade,
betx,alfx, mux,bety,alfy,muy,sx_mm,sy_mm,nsx_mm,nsy_mm,x,px,y,py,t,pt,dx,dpx,dy,dpy;   ! name, and basic twiss,   order as without select

if( twiss_select_magn == 1 )
{
  select, flag=twiss, column=l,lrad,angle,k1l, k1s, k2l,k2s, tilt, e1, e2, fint, fintx ;             ! basic magnet properties relevant for bends, quads and orbit correctors
}

if( twiss_select_aper == 1 )
{
  select, flag=twiss, column=apertype,aper_1,aper_2,aper_3,aper_4,comments;              ! aperture,   and  comments, like "Thick=1,Material=Cu"   which should not contain blanks
}

if( twiss_select_sol_corr==1 )
{
  select, flag=twiss, column=ksi,hkick,vkick; ! ksi for solenoids and orbit correctors
}

if( twiss_select_rf==1 )
{
  select, flag=twiss, column=volt, lag, freq, harmon;                           ! RF
}

if( twiss_select_Wfun==1 )
{
  select, flag=twiss, column=Wx,Wy;                                             ! Montague chromatic amplitude functions
}

if( twiss_select_rmat==1 )
{
  select, flag=twiss, column=re11,re12,re13,re14,re15,re16,re21,re22,re23,re24,re25,re26,re31,re32,re33,re34,re35,re36,re41,re42,re43,re44,re45,re46,re51,re52,re53,re54,re55,re56,re61,re62,re63,re64,re65,re66; ! 6x6 transfer matrix
}

if( twiss_select_coup==1 )
{
  select, flag=twiss, column=r11,r12,r21,r22; ! twiss coupling -- not to confuse with transfer map
}

if( select_sect==1 )
{
  select,flag=my_sect_table,column=pos,k1,k2,k3,k4,k5,k6,r11,r12,r13,r14,r15,r16,r21,r22,r23,r24,r25,r26,r31,r32,r33,r34,r35,r36,r41,r42,r43,r44,r45,r46,r51,r52,r53,r54,r55,r56,r61,r62,r63,r64,r65,r66; ! kick vector and 6x6 sector matrix, goes to extra sectormap file
}

! twiss,chrom,rmatrix,sequence=SeqName,file="FileName.tfs"; ! twiss call with chrom to get synrad integrals, and rmatrix
! twiss,chrom,rmatrix,beta0=StartValues,sectormap,sectortable=my_sect_table,sectorfile="FileName_sectormap.tfs",file="FileName_i.tfs"; // beamline twiss with rmatrix and sectormap
!---- note that chrom doubles the output to sectorfile   to avoid that call without chrom
!    twiss, rmatrix,beta0=StartValues,sectormap,sectortable=my_sect_table,sectorfile="FileName_sectormap.tfs",file="FileName_i.tfs"; // beamline twiss with rmatrix and sectormap
